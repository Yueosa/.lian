import QtQuick
import QtQuick.Layouts
import Quickshell
import qs.Services
import qs.config

Rectangle {
    id: root
    
    property bool isHovered: mouseArea.containsMouse
    
    implicitHeight: 28
    implicitWidth: isHovered ? (layout.width + 20) : 28
    radius: height / 2 
    color: Colorscheme.primary_container 

    Behavior on implicitWidth { NumberAnimation { duration: 300; easing.type: Easing.OutCubic } }

    RowLayout {
        id: layout
        anchors.centerIn: parent
        spacing: Sizes.spacing.s
        width: isHovered ? implicitWidth : iconText.implicitWidth

        Text {
            id: iconText
            font.family: Sizes.fontFamilyMono 
            font.pixelSize: Sizes.font.lg 
            Layout.alignment: Qt.AlignVCenter
            color: Colorscheme.on_primary_container 
            text: {
                if (Network.activeConnectionType === "ETHERNET") return "󰈀";
                if (!Network.connected) return "󰤭"; 
                let strength = Network.signalStrength; 
                if (strength >= 80) return "󰤨";
                if (strength >= 60) return "󰤥";
                if (strength >= 40) return "󰤢";
                if (strength >= 20) return "󰤟";
                return "󰤯";
            }
        }

        Text {
            id: nameText
            text: Network.activeConnection 
            font.bold: true 
            font.pixelSize: Sizes.font.sm 
            color: Colorscheme.on_primary_container 
            Layout.alignment: Qt.AlignVCenter
            visible: root.isHovered
            opacity: root.isHovered ? 1.0 : 0.0
            Behavior on opacity { NumberAnimation { duration: 200 } }
        }
    }

    MouseArea {
        id: mouseArea
        anchors.fill: parent
        hoverEnabled: true
        cursorShape: Qt.PointingHandCursor 
        onClicked: {
            if (WidgetState.qsOpen && WidgetState.qsView === "network") {
                WidgetState.qsOpen = false;
            } else {
                WidgetState.qsView = "network";
                WidgetState.qsOpen = true;
            }
        }
    }
}
