import QtQuick
import QtQuick.Controls
import qs.config
import "../../JS/weather.js" as WeatherJS

Rectangle {
    id: root

    property var sourceModel
    property int maxItems: 16
    property var items: []
    property bool hasData: false
    property real itemWidth: width > 0 ? width / 6 : 122
    property real chartMax: 15

    readonly property real topPadding: 16
    readonly property real contentWidth: Math.max(width, items.length * itemWidth)
    readonly property real dayArrowY: 60
    readonly property real topBarBaseY: height * 0.56 - 6
    readonly property real bottomBarBaseY: height * 0.56 + 6
    readonly property real barHalfRange: Math.max(36, Math.min(64, height * 0.16))
    readonly property real nightArrowY: height - 70

    radius: Sizes.rounding.card
    color: Qt.rgba(Colorscheme.surface_container_high.r, Colorscheme.surface_container_high.g, Colorscheme.surface_container_high.b, 0.42)
    clip: true

    function dateLabel(epoch) {
        return epoch ? Qt.formatDateTime(new Date(epoch * 1000), "M/d") : "--"
    }

    function formatSpeedValue(value) {
        if (!WeatherJS.validNumber(value)) return "--"
        const rounded = Math.round(value * 10) / 10
        if (Math.abs(rounded - Math.round(rounded)) < 0.05) return Math.round(rounded).toString()
        return rounded.toFixed(1)
    }

    function beaufortLevel(speedMs) {
        if (!WeatherJS.validNumber(speedMs) || speedMs < 0.3) return 0
        if (speedMs < 1.6) return 1
        if (speedMs < 3.4) return 2
        if (speedMs < 5.5) return 3
        if (speedMs < 8.0) return 4
        if (speedMs < 10.8) return 5
        if (speedMs < 13.9) return 6
        if (speedMs < 17.2) return 7
        if (speedMs < 20.8) return 8
        if (speedMs < 24.5) return 9
        if (speedMs < 28.5) return 10
        if (speedMs < 32.7) return 11
        return 12
    }

    function windColor(speedMs) {
        const bf = beaufortLevel(speedMs)
        if (bf < 4) return "#72d572"
        if (bf < 6) return "#ffca28"
        if (bf < 8) return "#ffa726"
        if (bf < 10) return "#e52f35"
        if (bf < 12) return "#99004c"
        return "#7e0023"
    }

    function chartUpperBound(highest) {
        if (!WeatherJS.validNumber(highest) || highest <= 0) return 5
        if (highest <= 5) return 5
        if (highest <= 8) return 8
        if (highest <= 12) return 12
        if (highest <= 15) return 15
        return Math.ceil(highest / 5) * 5
    }

    function rebuild() {
        const list = []
        let highest = 0
        let validCount = 0
        const count = sourceModel && sourceModel.count ? Math.min(maxItems, sourceModel.count()) : 0
        for (let i = 0; i < count; ++i) {
            const dayItem = sourceModel.get(i) || ({})
            const dayPart = dayItem.day || ({})
            const nightPart = dayItem.night || ({})
            const daySpeed = Number(dayPart.windSpeedMs)
            const nightSpeed = Number(nightPart.windSpeedMs)
            if (WeatherJS.validNumber(daySpeed)) {
                highest = Math.max(highest, daySpeed)
                validCount += 1
            }
            if (WeatherJS.validNumber(nightSpeed)) {
                highest = Math.max(highest, nightSpeed)
                validCount += 1
            }
            list.push({
                time: dayItem.time || 0,
                dayText: WeatherJS.dayLabelCN(i, dayItem.time || 0),
                dateText: dateLabel(dayItem.time || 0),
                daySpeed: daySpeed,
                nightSpeed: nightSpeed,
                dayTextValue: formatSpeedValue(daySpeed),
                nightTextValue: formatSpeedValue(nightSpeed),
                dayDirection: Number(dayPart.windDirection),
                nightDirection: Number(nightPart.windDirection),
                dayColor: windColor(daySpeed),
                nightColor: windColor(nightSpeed),
                emphasized: i !== 0
            })
        }
        items = list
        chartMax = chartUpperBound(highest)
        hasData = validCount > 0
    }

    function barHeight(speed) {
        if (!WeatherJS.validNumber(speed) || speed <= 0 || chartMax <= 0) return 0
        return Math.max(10, Math.min(barHalfRange, barHalfRange * speed / chartMax))
    }

    onSourceModelChanged: rebuild()
    onWidthChanged: rebuild()
    onHeightChanged: rebuild()
    Component.onCompleted: rebuild()

    Connections {
        target: root.sourceModel
        ignoreUnknownSignals: true

        function onModelReset() { root.rebuild() }
        function onRowsInserted() { root.rebuild() }
        function onRowsRemoved() { root.rebuild() }
        function onDataChanged() { root.rebuild() }
    }

    Flickable {
        id: trendFlick

        anchors.fill: parent
        clip: true
        interactive: true
        boundsBehavior: Flickable.StopAtBounds
        flickableDirection: Flickable.HorizontalFlick
        contentWidth: root.contentWidth
        contentHeight: height

        Item {
            id: trendContent
            width: trendFlick.contentWidth
            height: trendFlick.height

            Rectangle {
                visible: root.items.length > 0
                x: 0
                y: 0
                width: root.itemWidth
                height: parent.height
                color: Qt.rgba(Colorscheme.surface_container_highest.r, Colorscheme.surface_container_highest.g, Colorscheme.surface_container_highest.b, 0.18)
            }

            Repeater {
                model: root.items

                Item {
                    required property var modelData
                    required property int index

                    x: index * root.itemWidth
                    width: root.itemWidth
                    height: root.height

                    readonly property color weekColor: modelData.emphasized
                                                       ? Colorscheme.on_surface
                                                       : Qt.rgba(Colorscheme.on_surface_variant.r, Colorscheme.on_surface_variant.g, Colorscheme.on_surface_variant.b, 0.78)
                    readonly property color dateColor: modelData.emphasized
                                                       ? Colorscheme.on_surface_variant
                                                       : Qt.rgba(Colorscheme.on_surface_variant.r, Colorscheme.on_surface_variant.g, Colorscheme.on_surface_variant.b, 0.62)
                    readonly property real dayBarHeight: root.barHeight(modelData.daySpeed)
                    readonly property real nightBarHeight: root.barHeight(modelData.nightSpeed)

                    Text {
                        anchors.horizontalCenter: parent.horizontalCenter
                        y: root.topPadding
                        text: modelData.dayText
                        color: parent.weekColor
                        font.family: Sizes.fontFamily
                        font.pixelSize: Sizes.font.lg
                        font.bold: modelData.dayText === "今天"
                    }

                    Text {
                        anchors.horizontalCenter: parent.horizontalCenter
                        y: root.topPadding + 22
                        text: modelData.dateText
                        color: parent.dateColor
                        font.family: Sizes.fontFamilyMono
                        font.pixelSize: Sizes.font.xsm
                    }

                    WindDirectionGlyph {
                        visible: WeatherJS.validNumber(modelData.dayDirection)
                        anchors.horizontalCenter: parent.horizontalCenter
                        y: root.dayArrowY
                        width: 44
                        height: 44
                        glyphColor: modelData.dayColor
                        directionDegrees: modelData.dayDirection
                    }

                    Text {
                        visible: WeatherJS.validNumber(modelData.daySpeed) && modelData.daySpeed > 0
                        anchors.horizontalCenter: parent.horizontalCenter
                        y: root.topBarBaseY - parent.dayBarHeight - 28
                        text: modelData.dayTextValue
                        color: Colorscheme.on_surface_variant
                        font.family: Sizes.fontFamilyMono
                        font.pixelSize: Sizes.font.md
                    }

                    Rectangle {
                        visible: parent.dayBarHeight > 0
                        width: 10
                        height: parent.dayBarHeight
                        x: (parent.width - width) / 2
                        y: root.topBarBaseY - height
                        radius: width / 2
                        color: Qt.rgba(Qt.color(modelData.dayColor).r, Qt.color(modelData.dayColor).g, Qt.color(modelData.dayColor).b, 0.96)
                    }

                    Rectangle {
                        visible: parent.nightBarHeight > 0
                        width: 10
                        height: parent.nightBarHeight
                        x: (parent.width - width) / 2
                        y: root.bottomBarBaseY
                        radius: width / 2
                        color: Qt.rgba(Qt.color(modelData.nightColor).r, Qt.color(modelData.nightColor).g, Qt.color(modelData.nightColor).b, 0.58)
                    }

                    Text {
                        visible: WeatherJS.validNumber(modelData.nightSpeed) && modelData.nightSpeed > 0
                        anchors.horizontalCenter: parent.horizontalCenter
                        y: root.bottomBarBaseY + parent.nightBarHeight + 8
                        text: modelData.nightTextValue
                        color: Colorscheme.on_surface_variant
                        font.family: Sizes.fontFamilyMono
                        font.pixelSize: Sizes.font.md
                    }

                    WindDirectionGlyph {
                        visible: WeatherJS.validNumber(modelData.nightDirection)
                        anchors.horizontalCenter: parent.horizontalCenter
                        y: root.nightArrowY
                        width: 44
                        height: 44
                        glyphColor: modelData.nightColor
                        directionDegrees: modelData.nightDirection
                    }
                }
            }

            MouseArea {
                id: dragArea
                x: trendFlick.contentX
                y: 0
                z: 20
                width: trendFlick.width
                height: trendFlick.height
                acceptedButtons: Qt.LeftButton
                preventStealing: true
                cursorShape: pressed ? Qt.ClosedHandCursor : Qt.OpenHandCursor

                property real lastMouseX: 0

                onPressed: function(mouse) {
                    lastMouseX = mouse.x
                }

                onPositionChanged: function(mouse) {
                    if (!pressed) return
                    const dx = mouse.x - lastMouseX
                    const maxX = Math.max(0, trendFlick.contentWidth - trendFlick.width)
                    trendFlick.contentX = Math.max(0, Math.min(maxX, trendFlick.contentX - dx))
                    lastMouseX = mouse.x
                }
            }
        }
    }

    Text {
        anchors.centerIn: parent
        visible: !root.hasData
        text: "风况数据暂不可用"
        color: Colorscheme.on_surface_variant
        font.family: Sizes.fontFamily
        font.pixelSize: Sizes.font.xl
    }
}
