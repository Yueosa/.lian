import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Clavis.Weather 1.0
import qs.config

Rectangle {
    id: root
    property real uiScale: 1.0

    property var sourceModel
    property real itemWidth: trendFlick.width > 0 ? trendFlick.width / 6 : 122
    property int maxItems: 16
    property int currentTab: 0

    radius: Math.round(26 * uiScale)
    color: Qt.rgba(Colorscheme.surface_container.r, Colorscheme.surface_container.g, Colorscheme.surface_container.b, 0.86)
    border.width: 1
    border.color: Qt.rgba(Colorscheme.outline_variant.r, Colorscheme.outline_variant.g, Colorscheme.outline_variant.b, 0.42)
    clip: true

    function modelCount() {
        return sourceModel && sourceModel.count ? Math.min(maxItems, sourceModel.count()) : 0
    }

    function itemAt(index) {
        return sourceModel && sourceModel.get ? sourceModel.get(index) : ({})
    }

    function valueAt(map, key, fallback) {
        const v = map ? map[key] : undefined
        return (v === undefined || v === null || isNaN(v)) ? fallback : Number(v)
    }

    function fmtTemp(value) {
        return value !== undefined && value !== null && !isNaN(value) ? Math.round(value) + "°" : "--"
    }

    function fmtPercent(value) {
        return value !== undefined && value !== null && !isNaN(value) ? Math.round(value) + "%" : "--"
    }

    function dayLabel(index, epoch) {
        if (index === 0) return "昨天"
        if (index === 1) return "今天"
        if (index === 2) return "明天"
        if (!epoch) return "--"
        const week = ["周日", "周一", "周二", "周三", "周四", "周五", "周六"]
        return week[new Date(epoch * 1000).getDay()]
    }

    function dateLabel(epoch) {
        return epoch ? Qt.formatDateTime(new Date(epoch * 1000), "M/d") : "--"
    }

    ColumnLayout {
        anchors.fill: parent
        anchors.margins: 0
        spacing: Math.round(10 * root.uiScale)

        ColumnLayout {
            Layout.fillWidth: true
            Layout.leftMargin: Math.round(14 * root.uiScale)
            Layout.rightMargin: Math.round(14 * root.uiScale)
            Layout.topMargin: Math.round(16 * root.uiScale)
            Layout.preferredHeight: Math.round(82 * root.uiScale)
            spacing: Math.round(10 * root.uiScale)

            RowLayout {
                Layout.fillWidth: true
                spacing: Sizes.spacing.sm

                Text {
                    text: "calendar_month"
                    color: Colorscheme.on_surface_variant
                    font.family: "Material Symbols Outlined"
                    font.pixelSize: Math.round(22 * root.uiScale)
                    Layout.alignment: Qt.AlignVCenter
                }

                Text {
                    text: "每日预报"
                    color: Colorscheme.on_surface
                    font.family: "Noto Sans CJK SC"
                    font.bold: true
                    font.pixelSize: Math.round(22 * root.uiScale)
                    Layout.alignment: Qt.AlignVCenter
                }

                Item { Layout.fillWidth: true }
            }

            RowLayout {
                Layout.fillWidth: true
                spacing: Sizes.spacing.m

                RowLayout {
                    spacing: Sizes.spacing.none

                    TabBtn { textStr: "天气情况"; tabIdx: 0; isFirst: true }
                    TabBtn { textStr: "空气质量"; tabIdx: 1 }
                    TabBtn { textStr: "风况"; tabIdx: 2; isLast: true }
                }

                Item { Layout.fillWidth: true }

                Rectangle {
                    Layout.preferredWidth: Math.round(36 * root.uiScale)
                    Layout.preferredHeight: Math.round(36 * root.uiScale)
                    Layout.alignment: Qt.AlignVCenter
                    radius: Math.round(18 * root.uiScale)
                    color: moreMouse.containsMouse ? Colorscheme.surface_container_highest : Colorscheme.surface_container

                    Behavior on color { ColorAnimation { duration: 150 } }

                    Text {
                        anchors.centerIn: parent
                        text: "refresh"
                        color: Colorscheme.on_surface_variant
                        font.family: "Material Symbols Outlined"
                        font.pixelSize: Math.round(20 * root.uiScale)
                    }

                    MouseArea {
                        id: moreMouse
                        anchors.fill: parent
                        hoverEnabled: true
                        onClicked: WeatherPlugin.refresh()
                    }
                }
            }
        }

        Item {
            Layout.fillWidth: true
            Layout.fillHeight: true

            Flickable {
                id: trendFlick

                anchors.fill: parent
                clip: true
                interactive: root.currentTab === 0
                boundsBehavior: Flickable.StopAtBounds
                flickableDirection: Flickable.HorizontalFlick
                contentWidth: Math.max(width, root.modelCount() * root.itemWidth)
                contentHeight: height
                visible: root.currentTab === 0
                property bool initialPositionApplied: false

                onContentXChanged: trendCanvas.requestPaint()

                function applyInitialPosition() {
                    if (initialPositionApplied) return
                    const count = root.modelCount()
                    if (count < 2) {
                        contentX = 0
                        initialPositionApplied = true
                        return
                    }
                    const maxX = Math.max(0, contentWidth - width)
                    if (maxX <= 0) {
                        contentX = 0
                        initialPositionApplied = true
                        return
                    }
                    contentX = Math.min(root.itemWidth, maxX)
                    initialPositionApplied = true
                }

                Component.onCompleted: applyInitialPosition()
                onContentWidthChanged: applyInitialPosition()
                onWidthChanged: applyInitialPosition()
                onVisibleChanged: {
                    if (visible) applyInitialPosition()
                }

                Item {
                    id: trendContent
                    width: trendFlick.contentWidth
                    height: trendFlick.height

                    property real columnWidth: root.itemWidth
                    property real topTextY: 8
                    property real topLabelSpacing: 3
                    property real dayIconSize: Math.max(46, Math.min(60, columnWidth * 0.46))
                    property real dayIconY: 56
                    property real chartTopInset: 166
                    property real chartBottomInset: Math.max(chartTopInset + 72, height - 126)
                    property real rainLabelY: chartBottomInset + 18
                    property real nightIconSize: dayIconSize
                    property real nightIconY: height - nightIconSize - 12
                    property real highTempTextY: 102
                    property real lowTempTextY: nightIconY - 30

                    Canvas {
                        id: trendCanvas
                        anchors.fill: parent
                        antialiasing: true

                        property real chartTop: trendContent.chartTopInset
                        property real chartBottom: trendContent.chartBottomInset

                        function pointX(index) {
                            return root.itemWidth * index + root.itemWidth / 2
                        }

                        function yAt(value, minValue, maxValue) {
                            return chartBottom - (value - minValue) / (maxValue - minValue) * (chartBottom - chartTop)
                        }

                        onPaint: {
                            const ctx = getContext("2d")
                            ctx.clearRect(0, 0, width, height)

                            const count = root.modelCount()
                            if (count < 2) return

                            let dayValues = []
                            let nightValues = []
                            let precipitationValues = []
                            let minTemp = 999
                            let maxTemp = -999

                            for (let i = 0; i < count; ++i) {
                                const item = root.itemAt(i)
                                const day = item.day || ({})
                                const night = item.night || ({})
                                const dayTemp = root.valueAt(day, "temperatureC", root.valueAt(item, "temperatureMaxC", NaN))
                                const nightTemp = root.valueAt(night, "temperatureC", root.valueAt(item, "temperatureMinC", NaN))
                                const pop = Math.max(
                                    root.valueAt(day, "precipitationProbability", 0),
                                    root.valueAt(night, "precipitationProbability", 0)
                                )

                                dayValues.push(dayTemp)
                                nightValues.push(nightTemp)
                                precipitationValues.push(pop)
                                if (!isNaN(dayTemp)) {
                                    minTemp = Math.min(minTemp, dayTemp)
                                    maxTemp = Math.max(maxTemp, dayTemp)
                                }
                                if (!isNaN(nightTemp)) {
                                    minTemp = Math.min(minTemp, nightTemp)
                                    maxTemp = Math.max(maxTemp, nightTemp)
                                }
                            }

                            if (maxTemp < minTemp) return
                            if (Math.abs(maxTemp - minTemp) < 0.1) {
                                maxTemp += 1
                                minTemp -= 1
                            }

                            ctx.beginPath()
                            for (let f = 0; f < count; ++f) {
                                const xFill = pointX(f)
                                const yFill = yAt(dayValues[f], minTemp, maxTemp)
                                if (f === 0) ctx.moveTo(xFill, yFill)
                                else ctx.lineTo(xFill, yFill)
                            }
                            for (let r = count - 1; r >= 0; --r) {
                                ctx.lineTo(pointX(r), yAt(nightValues[r], minTemp, maxTemp))
                            }
                            ctx.closePath()
                            const fillGradient = ctx.createLinearGradient(0, chartTop, 0, chartBottom)
                            fillGradient.addColorStop(0, "rgba(" + Math.round(Colorscheme.primary.r * 255) + "," + Math.round(Colorscheme.primary.g * 255) + "," + Math.round(Colorscheme.primary.b * 255) + ",0.12)")
                            fillGradient.addColorStop(1, "rgba(" + Math.round(Colorscheme.primary.r * 255) + "," + Math.round(Colorscheme.primary.g * 255) + "," + Math.round(Colorscheme.primary.b * 255) + ",0.02)")
                            ctx.fillStyle = fillGradient
                            ctx.fill()

                            for (let p = 0; p < count; ++p) {
                                const popValue = precipitationValues[p]
                                if (popValue <= 0) continue
                                const x = pointX(p)
                                const fadedBar = p === 0
                                const barTop = chartBottom - (chartBottom - chartTop) * Math.min(100, popValue) / 100
                                ctx.fillStyle = fadedBar
                                    ? Qt.rgba(Colorscheme.secondary.r, Colorscheme.secondary.g, Colorscheme.secondary.b, 0.10)
                                    : Qt.rgba(Colorscheme.secondary.r, Colorscheme.secondary.g, Colorscheme.secondary.b, 0.18)
                                ctx.beginPath()
                                roundedRect(ctx, x - 5, barTop, 10, chartBottom - barTop, 5)
                                ctx.fill()
                                ctx.fillStyle = fadedBar
                                    ? Qt.rgba(Colorscheme.primary.r, Colorscheme.primary.g, Colorscheme.primary.b, 0.42)
                                    : Colorscheme.primary
                                ctx.font = "bold 11px \"JetBrainsMono Nerd Font\""
                                ctx.textAlign = "center"
                                ctx.fillText(root.fmtPercent(popValue), x, trendContent.rainLabelY)
                            }

                            drawSeries(ctx, dayValues, minTemp, maxTemp, Colorscheme.primary, 4)
                            drawSeries(ctx, nightValues, minTemp, maxTemp, Colorscheme.secondary, 4)
                        }

                        function drawSeries(ctx, values, minValue, maxValue, color, lineWidth) {
                            for (let i = 1; i < values.length; ++i) {
                                const prevX = pointX(i - 1)
                                const prevY = yAt(values[i - 1], minValue, maxValue)
                                const x = pointX(i)
                                const y = yAt(values[i], minValue, maxValue)
                                const faded = i - 1 === 0 || i === 0

                                ctx.save()
                                if (ctx.setLineDash && i === 1) ctx.setLineDash([4, 3])
                                ctx.strokeStyle = withAlpha(color, faded ? 0.26 : 1)
                                ctx.lineWidth = lineWidth
                                ctx.lineJoin = "round"
                                ctx.lineCap = "round"
                                ctx.beginPath()
                                ctx.moveTo(prevX, prevY)
                                ctx.lineTo(x, y)
                                ctx.stroke()
                                ctx.restore()
                            }

                        }

                        function withAlpha(color, factor) {
                            return Qt.rgba(color.r, color.g, color.b, color.a * factor)
                        }

                        function roundedRect(ctx, x, y, w, h, r) {
                            ctx.moveTo(x + r, y)
                            ctx.lineTo(x + w - r, y)
                            ctx.quadraticCurveTo(x + w, y, x + w, y + r)
                            ctx.lineTo(x + w, y + h - r)
                            ctx.quadraticCurveTo(x + w, y + h, x + w - r, y + h)
                            ctx.lineTo(x + r, y + h)
                            ctx.quadraticCurveTo(x, y + h, x, y + h - r)
                            ctx.lineTo(x, y + r)
                            ctx.quadraticCurveTo(x, y, x + r, y)
                        }
                    }

                    Repeater {
                        model: root.modelCount()

                        delegate: Item {
                            x: root.itemWidth * index
                            width: root.itemWidth
                            height: trendContent.height
                            opacity: index === 0 ? 0.45 : 1

                            property var dayItem: root.itemAt(index)
                            property var dayPart: dayItem.day || ({})
                            property var nightPart: dayItem.night || ({})

                            Rectangle {
                                anchors.fill: parent
                                radius: Sizes.rounding.xxxl
                                color: "transparent"
                                border.width: 0
                            }

                            Column {
                                y: trendContent.topTextY
                                anchors.horizontalCenter: parent.horizontalCenter
                                width: parent.width
                                spacing: trendContent.topLabelSpacing

                                Text {
                                    width: parent.width
                                    text: root.dayLabel(index, dayItem.time)
                                    color: Colorscheme.on_surface
                                    font.family: "Noto Sans CJK SC"
                                    font.pixelSize: Math.round(16 * root.uiScale)
                                    font.bold: index === 1
                                    horizontalAlignment: Text.AlignHCenter
                                    elide: Text.ElideRight
                                }

                                Text {
                                    width: parent.width
                                    text: root.dateLabel(dayItem.time)
                                    color: Colorscheme.on_surface_variant
                                    font.family: "JetBrainsMono Nerd Font"
                                    font.pixelSize: Math.round(13 * root.uiScale)
                                    horizontalAlignment: Text.AlignHCenter
                                }
                            }

                            MeteoIcon {
                                anchors.horizontalCenter: parent.horizontalCenter
                                y: trendContent.dayIconY
                                width: trendContent.dayIconSize
                                height: trendContent.dayIconSize
                                weatherCode: root.valueAt(dayPart, "weatherCode", -1)
                                iconName: dayPart.iconName || ""
                                night: false
                                style: "fill"
                            }

                            Text {
                                width: parent.width
                                y: trendContent.highTempTextY
                                text: root.fmtTemp(root.valueAt(dayPart, "temperatureC", root.valueAt(dayItem, "temperatureMaxC", NaN)))
                                color: Colorscheme.on_surface
                                font.family: "JetBrainsMono Nerd Font"
                                font.pixelSize: Sizes.font.hero
                                font.bold: true
                                horizontalAlignment: Text.AlignHCenter
                            }

                            Text {
                                width: parent.width
                                y: trendContent.lowTempTextY
                                text: root.fmtTemp(root.valueAt(nightPart, "temperatureC", root.valueAt(dayItem, "temperatureMinC", NaN)))
                                color: Colorscheme.on_surface_variant
                                font.family: "JetBrainsMono Nerd Font"
                                font.pixelSize: Sizes.font.xxl
                                font.bold: true
                                horizontalAlignment: Text.AlignHCenter
                            }

                            MeteoIcon {
                                anchors.horizontalCenter: parent.horizontalCenter
                                y: trendContent.nightIconY
                                width: trendContent.nightIconSize
                                height: trendContent.nightIconSize
                                weatherCode: root.valueAt(nightPart, "weatherCode", -1)
                                iconName: nightPart.iconName || ""
                                night: true
                                style: "fill"
                            }
                        }
                    }

                    MouseArea {
                        id: dragArea
                        x: trendFlick.contentX
                        y: 0
                        z: 20
                        width: trendFlick.width
                        height: trendFlick.height
                        acceptedButtons: Qt.LeftButton
                        preventStealing: true
                        cursorShape: pressed ? Qt.ClosedHandCursor : Qt.OpenHandCursor

                        property real lastMouseX: 0

                        onPressed: function(mouse) {
                            lastMouseX = mouse.x
                        }

                        onPositionChanged: function(mouse) {
                            if (!pressed) return
                            const dx = mouse.x - lastMouseX
                            const maxX = Math.max(0, trendFlick.contentWidth - trendFlick.width)
                            trendFlick.contentX = Math.max(0, Math.min(maxX, trendFlick.contentX - dx))
                            lastMouseX = mouse.x
                        }
                    }
                }
            }

            Rectangle {
                anchors.fill: parent
                visible: root.currentTab === 1
                color: "transparent"

                DailyAirQualityTrendPane {
                    anchors.fill: parent
                    sourceModel: root.sourceModel
                }
            }

            Rectangle {
                anchors.fill: parent
                visible: root.currentTab === 2
                color: "transparent"

                DailyWindTrendPane {
                    anchors.fill: parent
                    sourceModel: root.sourceModel
                }
            }
        }
    }

    component TabBtn: Item {
        property string textStr: ""
        property int tabIdx: 0
        property bool isFirst: false
        property bool isLast: false
        property bool isActive: root.currentTab === tabIdx

        Layout.preferredWidth: textItem.implicitWidth + 30 + (btnMouse.pressed ? 8 : 0)
        Layout.preferredHeight: 36

        property real rLeft: (isActive || isFirst || btnMouse.pressed) ? 16 : 4
        property real rRight: (isActive || isLast || btnMouse.pressed) ? 16 : 4
        property color bgColor: isActive
                                ? Colorscheme.primary_container
                                : (btnMouse.containsMouse ? Colorscheme.surface_container_highest : Colorscheme.surface_container)

        Behavior on Layout.preferredWidth { NumberAnimation { duration: 250; easing.type: Easing.OutBack; easing.overshoot: 1.2 } }
        Behavior on bgColor { ColorAnimation { duration: 150 } }

        Rectangle {
            anchors.fill: parent
            radius: parent.rLeft > parent.rRight ? parent.rLeft : parent.rRight
            color: parent.bgColor

            Behavior on radius { NumberAnimation { duration: 200; easing.type: Easing.OutSine } }
        }

        Rectangle {
            anchors.left: (parent.rLeft < parent.rRight) ? parent.left : undefined
            anchors.right: (parent.rRight < parent.rLeft) ? parent.right : undefined
            anchors.top: parent.top
            anchors.bottom: parent.bottom
            width: parent.width / 2 + 5
            visible: parent.rLeft !== parent.rRight
            radius: parent.rLeft < parent.rRight ? parent.rLeft : parent.rRight
            color: parent.bgColor

            Behavior on radius { NumberAnimation { duration: 200; easing.type: Easing.OutSine } }
        }

        Text {
            id: textItem
            anchors.centerIn: parent
            text: parent.textStr
            font.family: "Noto Sans CJK SC"
            font.pixelSize: Sizes.font.lg
            font.bold: parent.isActive
            color: parent.isActive ? Colorscheme.on_primary_container : Colorscheme.on_surface_variant
            z: 2

            Behavior on color { ColorAnimation { duration: 150 } }
        }

        MouseArea {
            id: btnMouse
            anchors.fill: parent
            hoverEnabled: true
            onClicked: root.currentTab = parent.tabIdx
            z: 3
        }
    }

    Connections {
        target: root.sourceModel
        ignoreUnknownSignals: true
        function onModelReset() {
            trendFlick.initialPositionApplied = false
            trendFlick.applyInitialPosition()
            trendCanvas.requestPaint()
        }
        function onDataChanged() { trendCanvas.requestPaint() }
        function onRowsInserted() {
            trendFlick.initialPositionApplied = false
            trendFlick.applyInitialPosition()
            trendCanvas.requestPaint()
        }
        function onRowsRemoved() { trendCanvas.requestPaint() }
    }

    onSourceModelChanged: {
        trendFlick.initialPositionApplied = false
        trendFlick.applyInitialPosition()
        trendCanvas.requestPaint()
    }
    onWidthChanged: trendCanvas.requestPaint()
    onHeightChanged: trendCanvas.requestPaint()
}
